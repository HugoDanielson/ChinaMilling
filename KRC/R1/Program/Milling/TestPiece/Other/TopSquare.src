&ACCESS RVO
&REL 8
&PARAM DISKPATH = KRC:\R1\Program\Milling\TestPiece
DEF TopSquare ( )
   INT lnActSt,lnUsedBase,lnUsedTool,lnBaseNr
   INT lnJobNr, lnCounter,lnCounter2,lnSegments
   REAL lrAmountCut, lrWorkPieceSize,lrRadius,lrDepthOfCut
   FRAME lfZcut
   FRAME lfToolDiamShift
   FRAME lfStartMillingPos
   FRAME lfBaseShift
   FRAME lfBaseCycleZ
   FRAME lfBaseCycleShift
   FRAME lfCircAuxPoint1
   FRAME lfCircEndPoint1
   
   CONTINUE     
   ;IF ($T1 OR $T2)THEN
   GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
   INTERRUPT ON 3
   BAS (#INITMOV,0)
   lnActSt = 1
   PTP $POS_ACT
   PTP lAstartPos
   
   gbRobControlActive = FALSE
   
   ; Used Base and tool
   lnUsedBase = 11
   lnUsedTool = 11
   
   gnLastToolNr = 4
   lnBaseNr = 24
   
   gfMillingOffset = $NULLFRAME
   
   lfZcut = $NULLFRAME
   lfZcut.Z = 0
   lfBaseShift = {X 2,Y -1.5, Z -16.0,A 0,B 0,C 0}
   ;lfBaseShift = {X 18,Y 16, Z -3,A 0,B 0,C 0}
   ;lfBaseShift.x = lfBaseShift.x - grTool_Diameter[gnLastToolNr]/2
   ;lfBaseShift.y = lfBaseShift.y - grTool_Diameter[gnLastToolNr]/2
   
   ;lfBaseShift = {X 50,Y 48.5,Z -3,A 0,B 0,C 0}   
   lfBaseCycleZ = $NULLFRAME
   lfBaseCycleZ.Z = 0
   
   ;*************************************************************
   ;*                SPINDLE START + CUT PARAMS                 *
   ;*************************************************************
   grUsedRPM = GetMillingRPM(grTool_VC[gnLastToolNr], grTool_Diameter[gnLastToolNr])
   grUsedMillingSpeed = GetMillingSpeed(grUsedRPM, grTool_ToothCount[gnLastToolNr], grTool_FeedPerTooth[gnLastToolNr])
   
   SpindleRun(TRUE,grUsedRPM,#CLOCKWISE)
   
   lfBaseCycleZ = $NULLFRAME
   lfBaseCycleZ.Z = 0
   BASE_DATA[lnUsedBase] = BASE_DATA[lnBaseNr] : lfBaseShift : lfBaseCycleZ
   TOOL_DATA[lnUsedTool] = TOOL_DATA[gnLastToolNr] :{X 0,Y 0,Z 0,A 270,B 0,C 0}
   
   MoveSettings(lnUsedBase, lnUsedTool, 100, grUsedMillingSpeed)
   
   lrAmountCut = 24.0
   lrWorkPieceSize = 100.0
   ;lrRadius = (218.0/2)/2
   lrRadius = (lrWorkPieceSize-lrAmountCut*2)/2 + grTool_Diameter[gnLastToolNr]/2
   
   lrDepthOfCut = 0.0
   ;*************************************************************
   ;*                Main Material Removal PASS                 *
   ;*************************************************************
   LIN {X -50, Y -50, Z 0,A 0, B 0, C 0}
   
   REPEAT
      
      MillSquarePass(lrWorkPieceSize,lrAmountCut,lrDepthOfCut,grTool_Diameter[gnLastToolNr])
      
      lrDepthOfCut = lrDepthOfCut - 0.2
   UNTIL lrDepthOfCut == -0.4
   
   lnSegments = 1000
   lfWorkPos[2] = $NULLFRAME
   lfWorkPos[2].X = lrWorkPieceSize/2
   lfWorkPos[2].Y = lfWorkPos[2].X
   
   LIN {X -50, Y -50, Z 0,A 0, B 0, C 0}
   
   FOR lnCounter2 = 0 TO 5 
      
      
      FOR lnCounter = 0 TO lnSegments
         LIN getCircFrame(360,lrRadius+5,lnSegments,lnCounter,lfWorkPos[2]) C_VEL
      ENDFOR
      FOR lnCounter = 0 TO lnSegments
         LIN getCircFrame(360,lrRadius+2,lnSegments,lnCounter,lfWorkPos[2]) C_VEL
      ENDFOR
      FOR lnCounter = 0 TO lnSegments
         LIN getCircFrame(360,lrRadius,lnSegments,lnCounter,lfWorkPos[2]) C_VEL
      ENDFOR
      
      lfWorkPos[2].Z = lfWorkPos[2].Z - 0.2
   ENDFOR
   
   ;
   TRIGGER WHEN DISTANCE=1 DELAY=0 DO  SpindleRun(FALSE,0,#CLOCKWISE) PRIO=-1 
   MoveSettings(lnUsedBase, lnUsedTool, 100, 500)
   LIN_REL {Z 100} C_VEL #BASE
   
   BAS (#INITMOV,0)
   PTP lAstartPos
END

DEFFCT FRAME getCircFrame(lrAngle :IN,lrRadius :IN,lnDevPart :IN,lnActPoint :IN ,lfStartPoint :IN)
   REAL lrAngle,lrRadius
   INT lnDevPart,lnActPoint
   FRAME lfStartPoint,lfPoint
   
   lfPoint = $NULLFRAME
   lfPoint.X = -(lrRadius * COS((lrAngle/lnDevPart*lnActPoint)))
   lfPoint.Y = -(lrRadius * SIN((lrAngle/lnDevPart*lnActPoint)))
   
   RETURN lfStartPoint:lfPoint
   
ENDFCT

DEF MillSquarePass(lrFrameLength :IN,lrInCut :IN, lrDepthOfCut: IN, lrToolDiam: IN)
   
   DECL FRAME lfCorner[4]
   INT i
   REAL lrFrameLength, lrInCut, lrToolDiam
   REAL lrCurrentCut, lrAP , lrToolRad,lrDepthOfCut
   
   lrToolRad = lrToolDiam/2
   ;lrAP = 6.0
   lrAP = 12
   lrCurrentCut = 0.0
   
   FOR i = 1 TO 4 STEP 1
      lfCorner[i] = $NULLFRAME
      lfCorner[i].Z = lrDepthOfCut
   ENDFOR
   
   REPEAT 
      
      ;lfCorner[1](x,x)
      lfCorner[1].X = lrCurrentCut                  - lrToolRad
      lfCorner[1].Y = lrCurrentCut                  - lrToolRad
      
      ;lfCorner[2](320,x)
      lfCorner[2].X = lrFrameLength - lrCurrentCut  + lrToolRad
      lfCorner[2].Y = lrCurrentCut                  - lrToolRad
      
      ;lfCorner[3](320-x,320-x)
      lfCorner[3].X = lrFrameLength - lrCurrentCut  + lrToolRad
      lfCorner[3].Y = lrFrameLength -lrCurrentCut   + lrToolRad
      
      ;lfCorner[4](x,320)
      lfCorner[4].X = lrCurrentCut                  - lrToolRad
      lfCorner[4].Y = lrFrameLength - lrCurrentCut  + lrToolRad
      
      lrCurrentCut = lrCurrentCut+lrAP
      
      FOR i = 1 TO 4 STEP 1
         LIN  lfCorner[i] C_VEL
      ENDFOR
      LIN  lfCorner[1] C_VEL
      
   UNTIL lrCurrentCut >= lrInCut
   ;ENDFOR
END