&ACCESS RVO
&REL 9
&PARAM DISKPATH = KRC:\R1\Program\Milling
DEF Wood_Test ( )
   INT lnActSt,lnUsedBase,lnUsedTool,lnBaseNr
   INT lnJobNr, lncounter, lncounter2
   FRAME lfZcut
   FRAME lfToolDiamShift
   FRAME lfStartMillingPos
   FRAME lfBaseShift
   FRAME lfBaseCycleZ
   
   CONTINUE     
   ;IF ($T1 OR $T2)THEN
   GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
   INTERRUPT ON 3
   BAS (#INITMOV,0)
   lnActSt = 1
   PTP $POS_ACT
;;ptp xhome
   ;ENDIF
   
   ; PTP XHOME
   SpindleRun(grUsedRPM,#CLOCKWISE)
   
   PTP lAstartPos
   
   ; Used Base and tool
   lnUsedBase = 11
   lnUsedTool = 11
   
   gnLastToolNr = 4
   lnBaseNr = 21
   
   gfMillingOffset = $NULLFRAME
  
   
   lfZcut = $NULLFRAME
   lfZcut.Z = 0
   lfBaseShift = {X 0,Y 0,Z 0,A 0,B 0,C 0}   
   lfBaseCycleZ = $NULLFRAME
   lfBaseCycleZ.Z = 0
   
   ;*************************************************************
   ;*                      Mill TOP SURFACE                     *
   ;*************************************************************
   lfBaseCycleZ = $NULLFRAME
   lfBaseCycleZ.Z = 0
   BASE_DATA[lnUsedBase] = BASE_DATA[lnBaseNr] : lfBaseShift : lfBaseCycleZ
   TOOL_DATA[lnUsedTool] = TOOL_DATA[gnLastToolNr] :{X 0,Y 0,Z 0,A 270,B 0,C 0}
   
   MoveSettings(lnUsedBase, lnUsedTool, 100, grUsedMillingSpeed)
   ;; Move To PreposMillingPos Point P5 {5mm far from WP surface}
   lfToolDiamShift   = $NULLFRAME   
   lfToolDiamShift.X = 0
   lfToolDiamShift.Y = grTool_Diameter[4]
   lfToolDiamShift.Z = 5
   lfStartMillingPos = lfWorkPos[5] : lfToolDiamShift 
   LIN lfStartMillingPos
  
   ;; Move To Milling Point P5
   MoveSettings(lnUsedBase, lnUsedTool, 100, grUsedMillingSpeed)
   lfToolDiamShift   =   $NULLFRAME   
   lfToolDiamShift.x =  0
   lfToolDiamShift.Y =  grTool_Diameter[4]
   lfToolDiamShift.Z =  0
   LIN lfWorkPos[5]:lfToolDiamShift C_VEL
   
   ;; Move To Milling Point P6
   MoveSettings(lnUsedBase, lnUsedTool, 100, grUsedMillingSpeed)
   lfToolDiamShift   =   $NULLFRAME   
   lfToolDiamShift.x =  0
   lfToolDiamShift.Y =  0;-grtool_diameter[4]
   lfToolDiamShift.Z =  0
   LIN lfWorkPos[6]:lfToolDiamShift C_VEL
   
   ;; Move To Milling Point P6
   MoveSettings(lnUsedBase, lnUsedTool, 100, grUsedMillingSpeed)
   lfToolDiamShift   =   $NULLFRAME   
   lfToolDiamShift.x =  grtool_diameter[4] -10
   lfToolDiamShift.Y =  0;-grtool_diameter[4]
   lfToolDiamShift.Z =  0
   LIN lfWorkPos[6]:lfToolDiamShift C_VEL
    ;; Move To Milling Point P5
   MoveSettings(lnUsedBase, lnUsedTool, 100, grUsedMillingSpeed)
   lfToolDiamShift   =   $NULLFRAME   
   lfToolDiamShift.x =  grTool_Diameter[4] -10
   lfToolDiamShift.Y =  grTool_Diameter[4]
   lfToolDiamShift.Z =  0
   LIN lfWorkPos[5]:lfToolDiamShift C_VEL
   
   MoveSettings(lnUsedBase, lnUsedTool, 100, 500)   
   ;; End of Milling Move Out of the WP
   TRIGGER WHEN DISTANCE=1 DELAY=0 DO  SpindleSetSpeed(50) PRIO=-1 
   LIN_REL {Z 5} C_VEL #TOOL 
   
   LIN_REL {Z 100} C_VEL #BASE
   
   TOOL_DATA[lnUsedTool] = TOOL_DATA[gnLastToolNr] :{X 0,Y 0,Z 0,A 180,B 90,C -90}
   MoveSettings(lnUsedBase, lnUsedTool, 100, 500)  
   PTP lastartpos C_PTP    
   
END