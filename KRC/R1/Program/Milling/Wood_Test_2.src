&ACCESS RVO
&REL 23
&PARAM DISKPATH = KRC:\R1\Program\Milling
DEF Wood_Test_2 ( )
   INT lnActSt,lnUsedBase,lnActBase,lnUsedTool,lnBaseNr
   INT lnJobNr, lnCounter,lnCounter2
   FRAME lfZcut
   FRAME lfToolDiamShift
   FRAME lfStartMillingPos
   FRAME lfBaseShift
   FRAME lfBaseCycleZ
   FRAME lfBaseCycleShift
   
   CONTINUE     
   ;IF ($T1 OR $T2)THEN
   GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
   INTERRUPT ON 3
   BAS (#INITMOV,0)
   lnActSt = 1
   PTP $POS_ACT
   ;;ptp xhome
   ;ENDIF
   
   ; PTP XHOME
   SpindleRun(grUsedRPM,#CLOCKWISE)
   
   PTP lAstartPos
   
   ; Used Base and tool
   lnUsedBase = 11
   lnActBase = 12
   lnUsedTool = 11
   
   gnLastToolNr = 4
   lnBaseNr = 22
   
   gfMillingOffset = $NULLFRAME
   
   lfZcut = $NULLFRAME
   lfZcut.Z = 0
   lfBaseShift = {X 0,Y 0,Z 5,A 0,B 0,C 0}   
   lfBaseCycleZ = $NULLFRAME
   lfBaseCycleZ.Z = 0
   
   ;*************************************************************
   ;*                      Mill TOP SURFACE                     *
   ;*************************************************************
   lfBaseCycleZ = $NULLFRAME
   lfBaseCycleZ.Z = 0
   BASE_DATA[lnUsedBase] = BASE_DATA[lnBaseNr] : lfBaseShift : lfBaseCycleZ
   TOOL_DATA[lnUsedTool] = TOOL_DATA[gnLastToolNr] :{X 0,Y 0,Z 0,A 270,B 0,C 0}
   
   MoveSettings(lnUsedBase, lnUsedTool, 100, grUsedMillingSpeed)
   ;; Move To PreposMillingPos Point P1 {5mm far from WP surface}
   lfToolDiamShift   = $NULLFRAME   
   lfToolDiamShift.X = 0
   lfToolDiamShift.Y = 0;grTool_Diameter[4]
   lfToolDiamShift.Z = 5
   lfStartMillingPos = lfWorkPos[1] : lfToolDiamShift 
   LIN lfStartMillingPos
   
   lfBaseCycleShift = $NULLFRAME
   BASE_DATA[lnActBase] = BASE_DATA[lnUsedBase]
   FOR lnCounter = 1 TO 2
      FOR lnCounter2 = 1 TO 6
         ;; Move To Milling Point P1
         MoveSettings(lnActBase, lnUsedTool, 100, grUsedMillingSpeed)
         lfToolDiamShift   =   $NULLFRAME   
         lfToolDiamShift.x =  0;grTool_Diameter[4] 
         lfToolDiamShift.Y =  0;-grTool_Diameter[4] 
         lfToolDiamShift.Z =   - (0.5 *lnCounter)
         LIN lfWorkPos[1]:lfToolDiamShift C_VEL
         
         ;; Move To Milling Point P2
         MoveSettings(lnActBase, lnUsedTool, 100, grUsedMillingSpeed)
         lfToolDiamShift   =   $NULLFRAME   
         lfToolDiamShift.x =  0;grTool_Diameter[4] /2
         lfToolDiamShift.Y =  0;-grTool_Diameter[4] /2
         lfToolDiamShift.Z =   - (0.5 *lnCounter)
         LIN lfWorkPos[2]:lfToolDiamShift C_VEL
         
         ;; Move To Milling Point P2
         MoveSettings(lnActBase, lnUsedTool, 100, grUsedMillingSpeed)
         lfToolDiamShift   =   $NULLFRAME   
         lfToolDiamShift.x =  grTool_Diameter[4] -1;* 2
         lfToolDiamShift.Y =  0;grTool_Diameter[4]
         lfToolDiamShift.Z =   - (0.5 *lnCounter)
         LIN lfWorkPos[2]:lfToolDiamShift C_VEL
         
         ;; Move To Milling Point P1
         MoveSettings(lnActBase, lnUsedTool, 100, grUsedMillingSpeed)
         lfToolDiamShift   =   $NULLFRAME   
         lfToolDiamShift.x =  grTool_Diameter[4] -1 ;* 2
         lfToolDiamShift.Y =  0;-grTool_Diameter[4]
         lfToolDiamShift.Z =  - (0.5 *lnCounter)
         LIN lfWorkPos[1]:lfToolDiamShift C_VEL
        
         
         lfBaseCycleShift.X = lfBaseCycleShift.X +(grTool_Diameter[4] * 2 )-1
         BASE_DATA[lnActBase] = BASE_DATA[lnUsedBase] : lfBaseCycleShift
      ENDFOR
      LIN_REL {Z 5} C_VEL #TOOL 
      lfBaseCycleShift = $NULLFRAME
      lfBaseCycleShift.X = 0
      BASE_DATA[lnActBase] = BASE_DATA[lnUsedBase]
      MoveSettings(lnActBase, lnUsedTool, 100, grUsedMillingSpeed)
      ;; Move To PreposMillingPos Point P1 {5mm far from WP surface}
      lfToolDiamShift   = $NULLFRAME   
      lfToolDiamShift.X = 0
      lfToolDiamShift.Y = 0;grTool_Diameter[4]
      lfToolDiamShift.Z = 5
      lfStartMillingPos = lfWorkPos[1] : lfToolDiamShift 
      LIN lfStartMillingPos
      
   ENDFOR
   
   MoveSettings(lnUsedBase, lnUsedTool, 100, 500)   
   ;; End of Milling Move Out of the WP
   TRIGGER WHEN DISTANCE=1 DELAY=0 DO  SpindleSetSpeed(50) PRIO=-1 
   LIN_REL {Z 5} C_VEL #TOOL 
   
   LIN_REL {Z 100} C_VEL #BASE
   
   TOOL_DATA[lnUsedTool] = TOOL_DATA[gnLastToolNr] :{X 0,Y 0,Z 0,A 180,B 90,C -90}
   MoveSettings(lnUsedBase, lnUsedTool, 100, 500)  
   PTP lastartpos C_PTP    
   
   WAIT SEC 1
   SpindleStop() 
   
END