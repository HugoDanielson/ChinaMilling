&ACCESS RV2
&REL 3
&PARAM DISKPATH = KRC:\R1\TP\Milling
DEF R2_Milling ( )
   $CYCFLAG[cycEmStop]       = NOT $ALARM_STOP OR NOT gDiExtEmStop OR NOT gDiExtOpSafAckn
   
   IF($PRO_STATE1==#P_RESET) OR ($PRO_STATE1==#P_FREE) THEN
      ; gbRobControlActive = FALSE
   ENDIF
   
   IF $CYCFLAG[cycEmStop] THEN
      gDoQuickStop = TRUE
      oFreqconverterCtlw = gsSpindleWord.WaitForCommand
      oConverterSetSpeed = 0
      gISetSpeed = 0
      gbSpindleStart = FALSE
      IF( gDISpindleActSpeed == 0)THEN
         gDoSto1 = FALSE
         gDoSto2 = FALSE
         
      ENDIF
   ELSE
      gDoSto1 = TRUE
      gDoSto2 = TRUE
      gDoQuickStop = FALSE
      
      IF gbSpindleStart THEN  
         oSpindleOperMode = gnSpindleOperatinMode
         oConverterSetSpeed =  getSpindleSpeed(gISetSpeed)
         oFreqconverterCtlw = gsSpindleWord.RunSpindle
         
         ;; gDOSpindleEnable = TRUE
      ELSE
         IF( gDISpindleActSpeed <> 0)THEN
            oFreqconverterCtlw = gsSpindleWord.StopDec1
         ELSE
            oFreqconverterCtlw = gsSpindleWord.WaitForCommand
         ENDIF
         oConverterSetSpeed = 0
         gISetSpeed = 0
      ENDIF  
   ENDIF
END

DEFFCT INT getSpindleSpeed (lrSetSpindleSpeed :IN)
   INT lrSetSpindleSpeed
   
   IF gISetSpeed<0 THEN
      RETURN (lrSetSpindleSpeed/10+65535)
   ELSE
      RETURN (lrSetSpindleSpeed/10)
   ENDIF
   
   RETURN 0
ENDFCT


GLOBAL DEF SpindleRun (lbStartSpindle :IN,lrSpindleRPM :IN, FI_DIRECTION:IN)
   BOOL lbStartSpindle
   INT lrSpindleRPM
   DECL TYPMillDirection FI_DIRECTION
   IF lbStartSpindle THEN
      SWITCH FI_DIRECTION
         CASE #CLOCKWISE
            gISetSpeed = -lrSpindleRPM
         CASE #COUNTERCLOCKWISE
            gISetSpeed = lrSpindleRPM
         DEFAULT
            gISetSpeed = 0
      ENDSWITCH
      gbSpindleStart = TRUE
   ELSE
      gbSpindleStart = FALSE  
   ENDIF
   
END

GLOBAL DEF SpindleClamp(lbTooleReleaseReq :IN)   
   BOOL lbTooleReleaseReq
   gDOChangeToolEnable = TRUE
   
   WAIT FOR gDISpindleActSpeed == 0
   
   IF (gDISpindleActSpeed == 0)THEN
      IF NOT lbTooleReleaseReq THEN     
         gDOToolClamp        = FALSE
         gDOToolRelease      = TRUE
      ELSE
         gDOToolClamp        = TRUE
         gDOToolRelease      = FALSE
      ENDIF
      WAIT SEC 1
      
   ENDIF
   
   gDOChangeToolEnable = FALSE
   gDOToolClamp        = FALSE
   gDOToolRelease = FALSE
END
